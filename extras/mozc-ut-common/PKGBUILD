# Maintainer: VojtechStep <me at vojtechstep dot eu>
# Based on modified PKGBUILD by: Nocifer <apmichalopoulos at gmail dot com>
# Based on original PKGBUILD by: UTUMI Hirosi <utuhiro78 at yahoo dot co dot jp>
# Contributor: Felix Yan <felixonmars@gmail.com>
# Contributor: ponsfoot <cabezon dot hashimoto at gmail dot com>


## The UT dictionary's upstream url: 'http://linuxplayers.g1.xrea.com/mozc-ut.html'


## Helpful internal stuff
_commit=884d9f4b64fe97597643247f1bed1d4fc9ca5bbf
_major=2
_minor=26
_build=4303
_revision=102
_mozcver=${_major}.${_minor}.${_build}.${_revision}
_utdicdate=20210222
_utdicrel=1
_utdiclink=27629
_kendate=202101
_jigyosyodate=202101
_bldtype=Release

pkgname='mozc-ut-common-emacs'
pkgver=${_mozcver}.${_utdicdate}
pkgrel=1
pkgdesc='The Open Source edition of Google Japanese Input bundled with the UT dictionary'
arch=('i686' 'x86_64')
url='https://github.com/google/mozc'
license=('custom')
depends=()
makedepends=('clang' 'git' 'ninja' 'pkgconf' 'python')
conflicts=('mozc' 'mozc-ut' 'mozc-ut2' 'mozc-neologd-ut' 'mozc-neologd-ut+ut2' 'mozc-ut-unified' 'mozc-ut-united' 'mozc-ut-common')
provides=("mozc=${_mozcver}" "mozc-ut=${_mozcver}.${_utdicdate}")
source=("${pkgname}-git::git+https://github.com/google/mozc.git#commit=${_commit}"
        "https://osdn.net/downloads/users/27/${_utdiclink}/mozcdic-ut-${_utdicdate}.${_utdicrel}.tar.bz2"
        "https://osdn.net/projects/ponsfoot-aur/storage/mozc/ken_all-${_kendate}.zip"
        "https://osdn.net/projects/ponsfoot-aur/storage/mozc/jigyosyo-${_jigyosyodate}.zip")
sha256sums=('SKIP'
            '0b71c941d96e700a80ca4081113eea7c373247269917bcaf9ace9b66d56c455e'
            'ee2615602786c0cb0f2b161656dd6bbef690f5f87b1e433f8cf23136375cd871'
            '57b17c67913a32ceabfb5b78e6211772020b92e6a59606b4251676b108f552ea')

prepare() {
    cd ${pkgname}-git

    git submodule update --init --recursive

    # Avoid build errors (don't use libc++)
    # These should probably be included as options in GYP_DEFINES
    sed -i -e 's/-stdlib=libc++//' src/gyp/common.gypi
    sed -i -e 's/-lc++//' src/gyp/common.gypi

    # Add the UT dictionary
    cat ${srcdir}/mozcdic-ut-${_utdicdate}.${_utdicrel}/mozcdic*-ut-*.txt >> src/data/dictionary_oss/dictionary00.txt

    # Add latest ZIP codes
    PYTHONPATH="${PYTHONPATH}:src/" \
    python src/dictionary/gen_zip_code_seed.py --zip_code=${srcdir}/KEN_ALL.CSV --jigyosyo=${srcdir}/JIGYOSYO.CSV >> src/data/dictionary_oss/dictionary09.txt
}

build() {
    cd ${pkgname}-git/src

    _targets='server/server.gyp:mozc_server gui/gui.gyp:mozc_tool unix/emacs/emacs.gyp:mozc_emacs_helper'

    export GYP_DEFINES='document_dir=/usr/share/licenses/mozc enable_gtk_renderer=0'

    python build_mozc.py gyp --target_platform=Linux --noqt
    python build_mozc.py build -c ${_bldtype} ${_targets}
}

package() {
    install -Dm644 mozcdic-ut-${_utdicdate}.${_utdicrel}/COPYING        ${pkgdir}/usr/share/licenses/mozc/ut-dictionary

    cd ${pkgname}-git/src

    install -Dm644 ../LICENSE                                           ${pkgdir}/usr/share/licenses/mozc/mozc
    install -Dm644 data/installer/credits_en.html                       ${pkgdir}/usr/share/licenses/mozc/mozc-submodules

    install -Dm755 out_linux/${_bldtype}/mozc_emacs_helper              ${pkgdir}/usr/bin/mozc_emacs_helper
    install -Dm755 out_linux/${_bldtype}/mozc_server                    ${pkgdir}/usr/lib/mozc/mozc_server
    install -Dm755 out_linux/${_bldtype}/mozc_tool                      ${pkgdir}/usr/lib/mozc/mozc_tool

    echo "Installing Emacs package"
    install -Dm755 unix/emacs/mozc.el                                   ${pkgdir}/usr/share/emacs/site-lisp/mozc/mozc.el
    emacs --batch -f batch-byte-compile ${pkgdir}/usr/share/emacs/site-lisp/mozc/mozc.el
    emacs --batch --eval="(progn \
          (let ((backup-inhibited t) \
                (version-control 'never)) \
            (make-directory-autoloads \
              \"${pkgdir}/usr/share/emacs/site-lisp/mozc/\" \
              \"${pkgdir}/usr/share/emacs/site-lisp/mozc/mozc-autoloads.el\")))"
}
