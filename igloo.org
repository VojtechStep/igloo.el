#+TITLE: Igloo - The Emacs Glue
#+AUTHOR: VojtechStep
#+BABEL: :cache yes
#+PROPERTY: header-args :tangle init.el :eval never :noweb tangle

* About

#+begin_quote
/History is scraps of evidence joined by the glue of imagination/
#+end_quote
â€‹- First image on DuckDuckGo for the query "famous glue quote"

This repo is my literate Emacs configuration. If you are unfamiliar with what literate programming is, basically every code snippet in this file gets yoinked out and put into the =init.el= file, which then serves as an init script for Emacs. Writing the configuration in an org file allows for (in my opinion) better documentation and organization.

To achieve the best performance, the configuration requires a build step.

Running =make= will collect all the code into =init.el= (and =early-init.el=, more on that later) and byte compile it. Once I get my hands dirty with [[https://www.emacswiki.org/emacs/GccEmacs][native compilation]], I hope to make use of it too. It will also initialize the package manager and pull all dependencies, so if you add any remote packages, it will require an internet connection.

Running =make install= will put the compiled and source files into =$PREFIX/emacs/= (PREFIX is =build= by default, but see below). If you want to OVERWRITE your existing configuration, you can use =make install PREFIX=$HOME/.config=. This does NO BACKUP. Use at your own risk.

Note that because of Emacs' insufficient support for non-standard user configuration directories, it is explicitly not a supported use case to set PREFIX to anything else than =$HOME/.config=, even for the build step. It works /kinda/, but sometimes straight.el and native compilation get confused and still work as if the user config was in =$HOME/.config=. Also, since =early-init.el= can't be loaded from a non-standard location before creating a frame, many of its configurations straight up don't have any effect.

Here's a cherry-picked list of some of the packages I use:
- [[https://github.com/emacs-evil/evil][evil-mode]] for Vim keybindings
- [[https://github.com/emacs-evil/evil-collection][evil-collection]] to get Vim keybindings where they ain't
- [[https://github.com/raxod502/straight.el][straight.el]] for package management
- [[https://github.com/jwiegley/use-package][use-package]] for package configuration
- [[https://github.com/noctuid/general.el][general.el]] for keybinding management
- [[https://github.com/bbatsov/projectile/][projectile]] for project management
- [[https://github.com/raxod502/selectrum][selectrum]] for incremental narrowing
- [[https://github.com/magit/magit][Magit]] for interfacing with Git
- [[https://github.com/google/mozc][mozc]] for Japanese input
- [[https://github.com/company-mode/company-mode][company-mode]] for completion
- [[https://github.com/djcb/mu][mu4e]] for mail
- [[https://github.com/emacs-lsp/lsp-mode][lsp-mode]] for smart code completion and navigation
- [[https://github.com/flycheck/flycheck][flycheck]] for error reporting
- [[https://github.com/raxod502/apheleia][apheleia]] for code formatting
- [[https://www.gnu.org/software/emacs/erc.html][ERC]] as an IRC client
- [[https://github.com/skeeto/elfeed][elfeed]] as an RSS reader

** Dependencies

*** Build

- [[https://www.gnu.org/software/make/][GNU Make]]
- [[https://www.gnu.org/software/emacs/][GNU Emacs]] (only tested with versions 28+)

*** Runtime

- [[http://aspell.net/][Aspell]] (not a very hard requirement, [[https://www.cs.hmc.edu/~geoff/ispell.html][Ispell]] or [[http://hunspell.github.io/][Hunspell]] should work just fine)
- [[https://github.com/sharkdp/fd][fd]] (=find= replacement)
- [[https://github.com/BurntSushi/ripgrep][rg]] (=grep= replacement)
- [[https://github.com/direnv/direnv][direnv]] (directory-local environment variables)

** Personal configuration

Personal configuration is managed in [[file:room.org][room.org]], which is not tracked in version control, and it's used to configure such things as RSS feeds or email accounts.

The template [[file:room.org.tpl][room.org.tpl]] is provided to be filled by the user. Running =make room.org= will either copy the template into room.org, or alternatively, if an existing user configuration is found, it will copy it to room.org.new, and it will prevent the user from building the project until it is filled in and manually renamed to room.org.

Combining igloo.org and room.org is a little hacky. A temporary org file is created, and the two configurations are copied into it. Then, the temporary file is tangled and deleted. AFAIK this is the only way to do it, because 1) =#+INCLUDE:= directives are only resolved during exporting, not tangling, and 2) exporting to org files using =ox-org= strips header arguments from source blocks.

Because of this process, an important caveat is introduced: *top-level properties*, such as =#+PROPERTY: header-args :tangle no=, *don't work in room.org*. Instead, all content should be put under a first-level heading which contains a =:PROPERTIES:= drawer.

* Process

First thing in the file is going to be the file header. It's useful to add a little description and enable lexical binding (it's all the rage these days).

#+begin_src emacs-lisp
;;; Igloo --- The glue connecting my whole operating experience. YMMV -*- lexical-binding: t; -*-

;;; Commentary:
;; Read https://github.com/VojtechStep/igloo.el/blob/master/igloo.org
;;
;; DO NOT CHANGE THIS FILE
;; This file is generated from `igloo.org' and will be overwritten on
;; every invocation of `make'

;;; Code:

(eval-when-compile (require 'cl-macs))
(unless (featurep 'early-init)
  (load (eval-when-compile early-init-file)))
#+end_src

** Philosophy

This configuration is built on the simple principle of "do what you can at compile time". This means that we have to properly differentiate what happens at comptime (via =eval-when-compile=), what happens at runtime (via plain forms) and what happens during both (via =eval-and-compile=).

First, the package manager is installed and configured at compile time. At runtime, we only load it via its entry point, the bootstrap file.

The =use-package= clauses need to be evaluated at both comptime, to clone and build the packages, and at runtime to expose them to the system.

For this reason, we define a macro to substitute the =(use-package ...)= form with.

#+begin_src emacs-lisp
(eval-when-compile
  (defmacro igloo-use-package (&rest body)
    "Passes BODY to `use-package' and call it at comptime and runtime."
    (declare (indent defun))
    `(eval-and-compile
       ,(cons 'use-package body))))
#+end_src

** Conventions

- Prefer lazy loading
- Use =igloo-use-package= for builtin packages too
- Follow this keyword order for =igloo-use-package= clauses:
#+name: igloo-keyword-order
#+begin_src emacs-lisp :tangle no
'(:disabled
  :if
  :after
  :straight
  :load-path
  :demand
  :defer
  :mode
  :commands
  :hook
  :custom
  :custom-face
  :general
  :preface
  :init
  :config)
#+end_src
- Declare keybindings to package commands in its =igloo-use-package= declaration, via =:general=
- Use =igloo-leader= for keybindings that should be always available (such as =org-capture= or =consult-buffer=)
- Use =igloo-local-leader= for keybindings relevant to a specific major /or/ minor mode (such as =consult-outline= or =flycheck-next-error=)
- Put =(declare-function)='s and helper function declarations in the =:preface=
- Use =:custom= over =(setq ..)=
- Put arguments to =:hooks= on a new line and always wrap the triggering hooks in a list
- When a more thorough documentation of package configuration is necessary, split the section into two headings - Configuration and Installation. Then, set the [[https://orgmode.org/manual/Noweb-Reference-Syntax.html][=noweb-ref=]] header argument in the Configuration section, and put the =igloo-use-package= form in the Installation section, with the proper =<< ... >>= reference.
- Unless a different default is required, set language-specific =-offset= variables (the ones that handle tab width) to =tab-width=

I was torn between specifying the keybindings in the same place as the package or in a separate Keybindings section, which is what I was using in my previous config.

The pros of separating the keybindings is that you can see them all in one place, so finding potential conflicts is easier. However, the cons is that since the keybindings were applied before loading of the specific packages, sometimes the package would override your changes, and there was no easy way of deferring the binding.

In the end, I decided to bundle place the bindings with the relevant package. You can get an overview of the bound keys via =general-describe-keybindings=, and conflict checking can probably be implemented in the linter.

Aliasing the =-offset= variables usually produces a warning, because their original value is different from =tab-width=. For this reason, we introduce a macro to suppress this warning.

#+begin_src emacs-lisp
(eval-when-compile
  (defmacro igloo-link-offset (offset)
    `(let ((warning-suppress-log-types (list (list 'defvaralias 'losing-value ,offset))))
       (defvaralias ,offset 'tab-width))))
#+end_src

*** Linter

#+begin_src emacs-lisp :tangle lint-conv.el
(eval-when-compile
  (require 'cl-lib)
  (require 'subr-x))

(defconst lint-keyword-order
  <<igloo-keyword-order>>)

(define-error 'unknown-keyword "Unknown keyword encountered")
(define-error 'wrong-order "Keyword in wrong position")
(define-error 'simple-hook "Hook form is too simple")

(defun check-kw-order (form)
  (let ((remaining-kws lint-keyword-order)
        (package-name (cadr form)))
    (cl-loop for kw in form
             when (symbolp kw)
             when (eq (string-to-char (symbol-name kw)) ?:)
             do
             (while (not (eq kw (car remaining-kws)))
               (cond
                ((cdr remaining-kws)
                 (setq remaining-kws (cdr remaining-kws)))
                ((memq kw lint-keyword-order)
                 (signal 'wrong-order
                         (list kw package-name)))
                (t
                 (signal 'unknown-keyword
                         (list kw package-name))))))))

(defun check-hooks (form)
  "Checks conventional :hook usage in FORM.

TODO: Not all checks are covered.

Acceptable:
:hook
((prog-mode) . ace-jump-mode)

:hook
((prog-mode) . (lambda () (ace-jump-mode)))

:hook
((prog-mode text-mode) . ace-jump-mode)

:hook
((prog-mode text-mode) . (lambda () (ace-jump-mode)))

:hook
(((prog-mode) . ace-jump-mode)
 ((text-mode) . ace-jump-mode))

:hook
(((prog-mode) . (lambda () (ace-jump-mode)))
 ((text-mode) . (lambda () (ace-jump-mode))))

Unacceptable:
:hook prog-mode
:hook (prog-mode . ace-jump-mode)
:hook (prog-mode text-mode)
:hook ((prog-mode . ace-jump-mode)
       (text-mode . ace-jump-mode))"
  (let ((package-name (cadr form)))
    (cl-loop for rest on form
             when (eq (car rest) :hook)
             do
             (let ((hooks (cadr rest)))
               (cond
                ((not (and (consp hooks)
                           (consp (car hooks))))
                 (signal 'simple-hook (list package-name)))
                (t))))))

(defun lint-files (files)
  (with-temp-buffer
    (cl-loop for f in files
             when (file-readable-p f)
             do
             (insert-file-contents-literally f nil nil nil 'replace)
             (goto-char (point-min))
             (condition-case err
                 (while t
                   (let ((form (read (current-buffer))))
                     (when (listp form)
                       (cond
                        ((eq (car form) 'use-package)
                         (error "Don't use regular `use-package' for %s" (cadr form)))
                        ((eq (car form) 'igloo-use-package)
                         (check-kw-order form)
                         (check-hooks form))))))
               (end-of-file)
               (unknown-keyword
                (error "Unknown keyword: %s in package %s" (cadr err) (caddr err)))
               (wrong-order
                (error "Keyword in an unexpected position: %s in package %s" (cadr err) (caddr err)))
               (simple-hook
                (error "Hook form is not of expected conventional format in package %s" (cadr err)))))))

(defun lint-from-args ()
  (condition-case err
      (lint-files command-line-args-left)
    (t (message "%s" err)
       (kill-emacs 1))))
#+end_src

** Display

Emacs is generally weird with its rendering pipeline, and that's a can of worms I don't want to get into here. One big observable quirk it has is that when running in server mode, it does not initialize some variables relating to faces and such.

To interact with faces and fonts, one has to wait until at least the first frame is made, which is what this macro is for. It is also intelligent enough to recognize when no server is running, and in that case it executes the body immediately.

The macro is only available during compilation.

#+begin_src emacs-lisp
(eval-when-compile
  (defmacro igloo-run-with-frontend (&rest body)
    "Run BODY when there is a frontend.

Emacs is weird when starting as a server, ok?

Some things aren't available (like face definitions),
so you want to hook into `server-after-make-frame-hook'
\(which confusingly refers to terminal clients too),
but that doesn't fire when opening Emacs without a server...

This macro checks if a server is running, and if it is,
it adds BODY to the hook,
and removes it after the first client is created.

If a server is not running, which means that the current instance was launched
as a normal Emacs process, run BODY straight away."
    (let ((funcname (cl-gentemp "igloo--run-with-frontend-")))
      (macroexp-progn
       `((defun ,funcname ()
           ,@body
           (remove-hook 'server-after-make-frame-hook #',funcname))
         (if (daemonp)
             (add-hook 'server-after-make-frame-hook #',funcname)
           (,funcname)))))))
#+end_src

* Initialization

** Early init
:PROPERTIES:
:header-args: :tangle early-init.el
:END:

Emacs 27 added =early-init.el=, which is a file that gets loaded very early in the process (hence the name). All the code blocks in this section are tangled into the =early-init.el= file, not =init.el=.

A word of warning: the documentation states that =early-init.el= should be used for "customizing how the package system is initialized" and "customizations [...] that need to be set up before initializing", because "the early init file is read too early into the startup process".

From [[https://git.savannah.gnu.org/cgit/emacs.git/tree/etc/NEWS.27#n206][etc/NEWS.27]]:

#+begin_example
,** Emacs can now be configured using an early init file.
The file is called "early-init.el", in 'user-emacs-directory'.  It is
loaded very early in the startup process: before graphical elements
such as the tool bar are initialized, and before the package manager
is initialized.  The primary purpose is to allow customizing how the
package system is initialized given that initialization now happens
before loading the regular init file (see below).

We recommend against putting any customizations in this file that
don't need to be set up before initializing installed add-on packages,
because the early init file is read too early into the startup
process, and some important parts of the Emacs session, such as
'window-system' and other GUI features, are not yet set up, which could
make some customization fail to work.

#+end_example

I try not to abuse the early init system, but I do some UI stuff that makes sense to me.

As a good citizen, we start this file with a header.

#+begin_src emacs-lisp
;;; Igloo --- Early init file -*- lexical-binding: t; -*-

;;; Commentary:
;; Read https://github.com/VojtechStep/igloo.el/blob/master/igloo.org
;;
;; DO NOT CHANGE THIS FILE
;; This file is generated from `igloo.org' and will be overwritten on
;; every invocation of `make'

;;; Code:
(eval-when-compile (require 'cl-lib))
#+end_src

*** Startup Optimizations

Many of the startup optimizations were inspired by [[https://github.com/hlissner/doom-emacs/blob/develop/docs/faq.org#how-does-doom-start-up-so-quickly][how Doom does it]]. I recommend reading it, since not all the tricks are used here, only the ones that noticeably improved the loading times for me.

In order to reduce the startup time of Emacs, we can employ several techniques. The most important part is deferred package loading, to which we will get in [[*Package management][Package management]].

**** GC Optimization

Other than that, we can start by looking into the garbage collector. The garbage collector runs when there is garbage to be picked up, that is when objects on the heap are being abandoned. We can influence when the garbage collection runs. If we wanted to have a lower memory footprint, we would want GC to run more frequently. However, in this case, we can live with a little memory spike if it provides us with a snappier experience.

Emacs garbage collector can run once the amount of allocated memory since the last GC run reaches a certain threshold, which is 8MB by default. This number is ridiculously small for most, so we increase it to about 100 megs.

Starting up is where a lot of garbage can be created, so it's easiest to pretty much disable garbage collection as soon as possible in the initialization, and re-enable it after Emacs starts. Another small-object sensitive workflows are minibuffer operations and company completion, so disable GC during those two too.

Furthermore, when enabling garbage collection, do so in a deferred manner, that is run it only after a second passes since the task (minibuffer action, completion) ends. This way, the thing running just after will still be free of garbage collection.

#+begin_src emacs-lisp
(defconst igloo--gc-threshold (* 100 1024 1024))
(setq gc-cons-threshold most-positive-fixnum
      gc-cons-percentage 0.6
      read-process-output-max (* 1024 1024))

(defun igloo--startup-cleanup ()
  "Reset settings disabled for faster startup."
  (setq gc-cons-threshold igloo--gc-threshold
        gc-cons-percentage 0.1))
(add-hook 'emacs-startup-hook #'igloo--startup-cleanup)


(defun igloo--gc-disable (&rest _)
  "Disable garbage collection."
  (setq gc-cons-threshold most-positive-fixnum))
(add-hook 'minibuffer-setup-hook #'igloo--gc-disable)
(add-hook 'company-completion-started-hook #'igloo--gc-disable)

(defun igloo--gc-enable ()
  "Enable garbage collection."
  (setq gc-cons-threshold igloo--gc-threshold))
(defun igloo--defer-gc-enable (&rest _)
  "Enable garbage collection, defered."
  (run-at-time 1 nil #'igloo--gc-enable))
(add-hook 'minibuffer-exit-hook #'igloo--defer-gc-enable)
(add-hook 'company-completion-finished-hook #'igloo--defer-gc-enable)

#+end_src

**** Other optimizations

We can make more optimizations by telling Emacs not to do stuff we manage ourselves.

For starters, the look and feel (things like non-blinking cursor, menu bar, font, ...) are configured in this file, not via X resources, so throw the loading of those resources out of the equation.

We also don't use the =vc= framework for controlling various version control systems, so pretend it doesn't exist by never enabling it, ever (from docs: "An empty list disables VC altogether").

Another important feature we throw out the window is Emacs builtin package management. It would otherwise initialize itself during startup, and that's just not groovy.

#+begin_src emacs-lisp
(advice-add #'x-apply-session-resources :override #'ignore)

(setq vc-handled-backends nil)

(setq package-enable-at-startup nil)
#+end_src

Emacs has support for bidirectional text, which I don't have a use case for, and disabling it can improve redisplay performance.

#+begin_src emacs-lisp
(setq bidi-inhibit-bpa t)
(setq-default bidi-paragraph-direction 'left-to-right)
#+end_src

Stuff from my previous early-init I either didn't understand or don't think I need any more, keeping it here for reference (not tangled):

#+begin_src emacs-lisp :tangle no
;; This was in early-init, supposed to help with startup, maybe try and bench it
;; Reference: https://github.com/hlissner/doom-emacs/blob/develop/docs/faq.org#unset-file-name-handler-alist-temporarily
(defvar igloo--file-name-handler-alist file-name-handler-alist)
(setq file-name-handler-alist nil)

;; I keep going back and forth on this
(setq suggest-key-bindings nil)

;; Font rendering performance tips
(setq-default font-lock-support-mode 'jit-lock-mode)
(setq-default font-lock-multiline t)
#+end_src

*** Early visual stuff

As mentioned, the GNU developers warn against using early init for graphical stuff. In this section, we are using early init for graphical stuff. Most of the settings are here to prevent their initialization, because it would just be wasteful to initialize them and then disable them later.

First of all, we set the default parameters of new frames. Since early init is done so early, they also apply to the first created frame. We hide all the scrollbars, menu bars and tool bars and set the default font and background color.

We also disable the blinking cursor, because who came up with that?

#+begin_src emacs-lisp
(setq default-frame-alist
      (append
       '((vertical-scroll-bars . nil)
         (horizontal-scroll-bars . nil)
         (font . "JetBrains Mono") ;(ref:default-font)
         (background-color . "#1d1f21")) ;(ref:frame-bg)
       default-frame-alist))

(set-face-attribute 'default nil :height 130)
(setq menu-bar-mode nil
      tab-bar-mode nil
      tool-bar-mode nil
      blink-cursor-mode nil)
#+end_src

We set the font because it doesn't make sense to let Emacs load a font that's going to get replaced later. Depending on the font you choose, it might be necessary to change the default height, which is counted in 0.1pt.

The background color is hardcoded and you should set it to the same background color as your main theme. It's set here because we want to avoid another color flashing the frame before the theme is loaded[fn:1].

We could also set the =menu-bar-lines= and =tool-bar-lines= frame parameters to 0 to disable the menu bar and tool bar. Instead, we disable them by settings the variables =menu-bar-mode= and =tool-bar-mode=. The effect is the same, because the modes work by setting the frame parameters, but also Emacs doesn't think those mode are enabled when they aren't.

Furthermore, =frame-resize-pixelwise= makes Emacs not align the window size to character size. This is useful, because otherwise Emacs might not play along with tiling window managers. =frame-inhibit-implied-resize= set to t disables implicit resizing of the frame by for example enabling the toolbar, the scrollbar or changing fonts. The default setting is to preserve the number of characters shown on screen, not the window size.

We also tell Emacs that we don't want to compact font caches. This might cause the memory usage to grow, especially if we were using a lot of fonts (which we don't), but the GC and redisplay can be a little faster (since GC doesn't compact font caches and redisplay doesn't have to reopen them again).

#+begin_src emacs-lisp
(setq-default frame-resize-pixelwise t
              frame-inhibit-implied-resize t
              inhibit-compacting-font-caches t)
#+end_src

#+begin_src emacs-lisp
(provide 'early-init)
;;; early-init.el ends here
#+end_src

** Package management

First, to be able to install packages, bootstrap [[https://github.com/raxod502/straight.el][straight.el]] and [[https://github.com/jwiegley/use-package][use-package]].

Configure straight to run on the bleeding edge and use ssh by default to clone repositories. Those need to be =defvar='s, because neither straight or use-package have been loaded at this point.

#+begin_src emacs-lisp
(eval-and-compile
  (defvar straight-repository-branch "develop")
  (defvar straight-vc-git-default-protocol 'ssh)
  (defvar straight-vc-git-default-clone-depth 1))
#+end_src

Straight can also automatically recompile packages if one chooses to edit the locally checked-out sources. By default, it checks at startup if the source files were changed, which can hurt startup performance. Change it to only check for edits when a file is saved, and when explicitly checking with =straight-check-package= or =straight-check-all=.

#+begin_src emacs-lisp
(defvar straight-check-for-modifications '(find-when-checking check-on-save))
#+end_src

Also, configure use-package to defer by default (without having to specify =:defer t=) and capture statistics (which can be shown by calling =use-package-report=). Verbosity is useful for debugging, but generally not necessary.

Deferring package loading is useful to improve startup time, because it means that the packages themselves aren't loaded and initialized on startup, only their autoloads are (autoloads are a mechanism for a file to export functions that trigger a loading of the file they are defined in when called).

The =use-package-verbose= is useful when you want to diagnose a problem with package loading - for example to see which packages take a long time to load, or when are packages loaded and configured.

We instruct use-package to not emit error handling code when expanding macros. They are unnecessary when one knows their config works, which is the case here, and it helps with debugging macro-expanded code. +It also allows the byte-compiler to properly process the =:functions= block of use-package+ Apparently, when byte-compiling, use-package still emits some comptime-only error checking code that trips up the byte compiler, so the =:functions= clause doesn't really work as intended, and it is still necessary to add =declare-function='s to the =:preface=.

#+begin_src emacs-lisp
(eval-and-compile
  (defvar use-package-compute-statistics t)
  (defvar use-package-always-defer t)
  (defvar use-package-verbose nil)
  (defvar use-package-expand-minimally t)
  (defvar use-package-use-theme nil))
#+end_src

The bootstrap file location has to be accessible at both comptime and runtime.

#+begin_src emacs-lisp
(eval-when-compile
  (defconst igloo-straight-bootstrap
    (eval-when-compile
      (expand-file-name "straight/repos/straight.el/bootstrap.el" user-emacs-directory))))
#+end_src

Install the package manager if it can't be found. This only happens at compile time. At runtime, it is assumed that the package manager, and all other packages for that matter, are installed.

#+begin_src emacs-lisp
(eval-when-compile
  (unless (file-exists-p igloo-straight-bootstrap)
    (with-current-buffer
        (url-retrieve-synchronously
         "https://raw.githubusercontent.com/raxod502/straight.el/develop/install.el"
         'silent 'inhibit-cookies)
      (goto-char (point-max))
      (eval-print-last-sexp))))
#+end_src

Once the package manager is guaranteed to be present at comptime and runtime, load it and setup =use-package=.

First, download and compile it. Once it's present on the system, require it. The call to =straight-use-package-mode= is necessary, because it adds keywords to =use-package-keyword=, but the variable hasn't been defined when loading =use-package=.

#+begin_src emacs-lisp
(eval-and-compile
  (load (eval-when-compile igloo-straight-bootstrap))
  (require 'straight)
  (straight-use-package 'use-package)

  (require 'use-package)
  (straight-use-package-mode t))
#+end_src

Now that the package manager is loaded, start configuring packages.

*** TODO Managing globally installed packages

I still haven't figured out how to make straight cooperate properly with local non-vcs directories. For example, mu4e and mozc require other system components, like native binaries, to be built, so they are often included in the distro's package manager. It would be nice if one didn't have to have two copies of the repo on the disk - one from the system package manager and one from straight. I use =:load-path= for those packages for now, but I loose on the nice automatic builds and such.

** Execution environment

I struggled a little with figuring out how to keep the environment variables used in Emacs the same as in the rest of the system. I even wrote a script that was valid fish and elisp at the same time! [[https://github.com/VojtechStep/configs/blob/b416fc4b2236374c2f88f601a076736a57b2700b/envs.el][Check it out]]!

In the end I resigned, and now I spawn a shell process to extract the environment from. Turns out it's not that big of a deal, since my fish shell starts in about 8ms.

We already changed the default shell [[(default-shell)][here]], so we need to tell the package that we want the environment extracted from the user's default shell (=$SHELL=).

The list of environment variables is something you probably want to modify for yourself.

#+begin_src emacs-lisp
(igloo-use-package exec-path-from-shell
  :straight t
  :demand
  :custom
  (exec-path-from-shell-shell-name (getenv "SHELL"))
  (exec-path-from-shell-arguments nil)
  (exec-path-from-shell-variables
   '("PATH"
     "MANPATH"
     "DISPLAY"
     "CXX"
     "CC"
     "XDG_CONFIG_HOME"
     "XDG_CACHE_HOME"
     "XDG_DATA_HOME"
     "XAUTHORITY"
     "GNUPGHOME"
     "DOTFILES_HOME"
     "CARGO_HOME"
     "RUSTUP_HOME"
     "STACK_ROOT"
     "DOCKER_CONFIG"
     "TERMINFO"
     "SCREENSHOT_DIR"
     "PYTHONSTARTUP"
     "NPM_CONFIG_USERCONFIG"
     "BROWSER"
     "FZF_DEFAULT_COMMAND"
     "FZF_DEFAULT_OPTS"
     "LESSHISTFILE"
     "LESS"
     "DOTNET_CLI_TELEMETRY_OPTOUT"
     "EMAIL"
     "NIX_PATH"))
  :config
  (exec-path-from-shell-initialize))
#+end_src

** Keybinding groundwork

Use [[https://github.com/justbur/emacs-which-key][which-key]] for showing available keybindings when a chord is not complete.

#+begin_src emacs-lisp
(igloo-use-package which-key
  :straight t
  :demand
  :custom
  (which-key-idle-delay 0.6)
  :config
  (which-key-mode))
#+end_src

The keybindings in Igloo are managed by [[https://github.com/noctuid/general.el][general.el]]. It allows one to create a /definer/ to be used in subsequent bindings - in this case the =igloo-leader=, bound to ~SPC~.

I don't use it here, but one can specify not only =:prefix=, but also a =:global-prefix= keyword for a definition. The difference is that the =:global-prefix= chord is used for "special states", like insert or emacs states, where the set of unbound keys might be drastically different (for example you wouldn't want to bind ~SPC~ in insert mode).

#+begin_src emacs-lisp
(eval-when-compile
  (defmacro igloo-lcag (key)
    "Append LCtrl, Alt and GUI before KEY."
    `(concat "C-M-s-" ,key)))

(igloo-use-package general
  :straight t
  :demand
  :config
  (eval-when-compile
    (general-create-definer igloo-leader
      :states '(motion normal)
      :keymaps 'override
      :prefix "SPC")
    (general-create-definer igloo-local-leader
      :states 'motion
      :keymaps 'override
      :prefix "\\"))
  (igloo-leader
    "" '(nil :wk "major-mode agnostic leader")
    "C-g" '(keyboard-quit :wk "abort")
    "a" '(nil :wk "apps")
    "c" '(nil :wk "close")
    "g" '(nil :wk "buffer control"))
  (igloo-local-leader
    "" '(nil :wk "major-mode specific leader")
    "C-g" '(keyboard-quit :wk "abort")))
#+end_src

[[https://github.com/magit/transient][Transient]] seems like a really nice interface, and I want to investigate its usage in the future. For now, just enable the quit-on-q feature.

#+begin_src emacs-lisp
(igloo-use-package transient
  :straight t
  :preface
  (declare-function transient-bind-q-to-quit "transient")
  :config
  (transient-bind-q-to-quit))
#+end_src

*** Basic keybindings

This section contains package declarations for built-in functionality that only have associated keybindings.

#+begin_src emacs-lisp
(igloo-use-package newcomment
  :general
  (:states '(motion insert)
   "C-;" #'comment-dwim))
#+end_src

#+begin_src emacs-lisp
(igloo-use-package simple
  :general
  (:states 'insert
   "C-v" #'yank
   "C-S-v" #'quoted-insert)
  (:states 'visual
   "gl" #'count-words-region))
#+end_src

#+begin_src emacs-lisp
(igloo-use-package align
  :general
  (:states 'visual
   "<tab>" #'align))
#+end_src

#+begin_src emacs-lisp
(igloo-use-package files
  :general
  (igloo-leader
    "cc" '(bury-buffer :wk "close buffer")
    "gr" '(revert-buffer :wk "revert buffer")
    "gR" '(rename-buffer :wk "rename buffer")))
#+end_src

* UI/UX

** Behaviour

As for the appearance, I'm not a huge fan of the startup screen, so disable it.

I also consider myself a 1337 h4x0r, so don't nag me about advanced-level commands.

I use [[https://github.com/raxod502/selectrum][selectrum]] as a completion framework (more on that [[*Selection framework][later]]), so I don't care much about shorter ways I can type in commands.

#+begin_src emacs-lisp
(setq inhibit-startup-screen t
      disabled-command-function nil
      extended-command-suggest-shorter nil)
#+end_src

This adds VIM-like (read: correct) scrolling behavior: only scroll one line at a time, keep a fixed number of visible lines around the cursor and /never/ recenter when the cursor goes off screen (honestly, why is that even a thing).

#+begin_src emacs-lisp
(setq-default scroll-step 1
              scroll-margin 3
              scroll-conservatively 101)
#+end_src

There is no yes/no question important enough to require up to three key presses more then necessary, so make all yes/no prompts into y/n prompts.

#+begin_src emacs-lisp
(defalias 'yes-or-no-p 'y-or-n-p)
#+end_src

When an action grows the minibuffer (for example org-drill), I don't want it to stay enlarged after the action finishes, which is the default behaviour.

#+begin_src emacs-lisp
(setq-default resize-mini-windows t)
#+end_src

I mostly use the keyboard to navigate my system, and it's a little frustrating when I have a hidden mouse pointer somewhere on the screen and it causes a part of a buffer to be highlighted. Ergo, only apply the hover overlay when the mouse is moved.

#+begin_src emacs-lisp
(setq-default mouse-highlight 1)
#+end_src

Do you like polluting your working directory with files like =.#totaly-a-file.rs= and =existing-file.zig~=? Yeah, me neither, so disable them pesky lockfiles and backup files.

#+begin_src emacs-lisp
(setq create-lockfiles nil)
(setq auto-save-default nil)
(setq backup-directory-alist `(("." . ,(locate-user-emacs-file "saves/"))))
#+end_src

The fish shell, which I use, is not POSIX compliant, and has a bunch of bells and whistles that I don't need when spawning inferior shell processes. Therefore, I prefer to use the standard =/bin/sh= as the default (which should be symlinked to dash).

#+begin_src emacs-lisp
(setq shell-file-name "/bin/sh") ;(ref:default-shell)
#+end_src

I prefer seeing code on my screen, not empty space - therefore, reduce the default tab width by a factor of 4.

The reason I prefer spaces over tabs is because when working with LISP-y languages, which look good when properly aligned, you could get a mix of tabs and spaces at the beginning of lines. Enabling truncated lines makes it so that by default, the text overflows off the screen, instead of breaking at the edge of the window.

#+begin_src emacs-lisp
(setq-default tab-width 2)
(setq-default indent-tabs-mode nil)
(setq-default sentence-end-double-space nil)
(setq-default truncate-lines t)
#+end_src

Emacs has built-in functionality for treating =CamelCase= and =snake_case= labels as consisting of separate "words", making it more natural to move around. Turn it on.

#+begin_src emacs-lisp
(igloo-use-package subword
  :demand
  :config
  (global-subword-mode))
#+end_src

Insert bracket pairs automatically.

#+begin_src emacs-lisp
(igloo-use-package elec-pair
  :demand
  :config
  (electric-pair-mode))
#+end_src

*** Window layout

At the moment, I am using [[https://github.com/baskerville/bspwm][bspwm]] as my window manager, because it does its job of laying out windows well. I find it more intuitive to delegate the feat of managing windows to the window manager, instead of having another set of keybindings and behaviour for just Emacs.

The basic setting that tells Emacs to use multiple X windows (or /frames/ in Emacs lingo) is =pop-up-frames=. When set to non-nil, it tells Emacs that it's OK to use multiple frames.

The second setting is =frame-auto-hide-function=. This is the function called when closing the last window of a frame. The default value of =iconify-frame= minimizes the frame, but on window managers that don't support window minimization, it just makes the frame unresponsive and it's confusing, and the correct behaviour is to get rid of the frame altogether.

#+begin_src emacs-lisp
(setq pop-up-frames 'graphic-only)
(setq frame-auto-hide-function #'delete-frame)
#+end_src

There is also the variable =display-buffer-alist=, which I don't think I fully understand to this day. I did use it in my previous config though, so once I start hitting some window layout issues, I will try to fix them and document them properly.

Now, some packages +crap their pants+ don't handle frames gracefully, so additional configuration is required.

**** Helpful

When Emacs is configured to pop up /windows/, then upon invocation, helpful splits the window, moves focus to the new window, and shows the help there. When looking up symbols from the helpful buffer, it reuses the previously focused window, and further jumps back and forth, closing the piled-on layers of helpful buffers when hitting ~q~.

When Emacs is configured to pop up /frames/, then upon invocation, helpful correctly pops out a new frame, but then spirals away into spawning many windows, sometimes quitting correctly, sometimes displaying non-help buffers, and it's very unpredictable.

To fix these issues, I decided on the following behaviour I want from the helpful windows, and implemented the showing and closing functions accordingly:

When showing help, first try to reuse a buffer that already shows a help buffer, otherwise create a new frame. When displaying in an existing frame, push itself on a "help stack".
When closing help, remove the current buffer from the stack. If there are no other buffers in the "help stack", close the frame. If the stack is not empty, show its head.

A potential improvement could be to only reuse the helpful frame if jumping from a helpful buffer - that is, always create a "helpful frame" when looking up something from code, but reuse the same frame when jumping through the documentation.

One thing to note about the implementation is that helpful needs the new window to be selected when =igloo--helpful-show-buffer= returns, otherwise you hit some failed assertions.

#+begin_src emacs-lisp
(defun igloo--helpful-show-buffer (buffer &optional _) ;(ref:helpful-show-buffer)
  (let ((window (display-buffer
                 buffer
                 '(display-buffer-reuse-mode-window
                   . ((mode . helpful-mode)))))
        (old-frame (selected-frame)))
    (if window
        (let ((frame (window-frame window)))
          (unless (eq frame old-frame)
            (select-frame-set-input-focus frame))
          (select-window window))
      (set-buffer buffer))
    buffer))
#+end_src

The quitting of the buffer needs to be reimplemented too, because the default =quit-window= only closes the frame if it only ever showed one buffer - if I were to navigate to a different helpful buffer (for example by calling =helpful-at-point=), it breaks the whole thing, and once all the helpful buffers are hidden with pressing ~q~, the frame doesn't close, but instead shows the buffer from where the original helpful buffer was created.

#+begin_src emacs-lisp
(defun igloo--helpful-quit-buffer ()
  (interactive)
  (let* ((window (selected-window))
         (buffer (window-buffer window))
         (prev-buffers (window-prev-buffers window)))
    (if (or (not prev-buffers)
            (eq (caar prev-buffers) (current-buffer)))
        (window--delete window)
      (switch-to-prev-buffer window 'bury)
      (bury-buffer-internal buffer))))
#+end_src

** Appearance

I do love myself a dark theme. I go with [[https://github.com/purcell/color-theme-sanityinc-tomorrow][Tomorrow night]]. Reminder: when changing a theme, we should change the initial background color of frames in [[(frame-bg)][frame parameters]].

#+begin_src emacs-lisp
(igloo-use-package color-theme-sanityinc-tomorrow
  :straight t
  :demand
  :config
  (load-theme 'sanityinc-tomorrow-night t))
#+end_src

Don't show a help message in every new frame.

#+begin_src emacs-lisp
(igloo-use-package server
  :custom
  (server-client-instructions nil))
#+end_src

Render a chunky boi when the cursor is over a tab.

#+begin_src emacs-lisp
(setq x-stretch-cursor t)
#+end_src

Show aggressive trailing whitespace in source files.

#+begin_src emacs-lisp
(require 'mode-local)
(setq-mode-local prog-mode
                 show-trailing-whitespace t)
(face-spec-set 'trailing-whitespace '((t . (:background "red1"))))
#+end_src

As mentioned previously, I rarely touch the mouse when operating Emacs. Therefore, graphical hover tooltips are useless, not to mention ugly. When tooltip mode is off, the information is printed in the echo-area, which I find preferable.

#+begin_src emacs-lisp
(igloo-use-package tooltip
  :config
  (tooltip-mode -1))
#+end_src

I try to use relative numbers for moving faster in the file - if you want to move to a line above your cursor, its easy to look at the number next to the line, and the hit ~<n>k~ for example.

You will sometimes see the form =(setq-default display-line-numbers 'visual)= instead. The difference is that when the mode is turned on, it performs some additional actions, such as looking at the number of lines in the file, and then setting the gutter width accordingly (that's what =-width-start= is for). Otherwise the text will jump around as you scroll.

#+begin_src emacs-lisp
(igloo-use-package display-line-numbers
  :demand
  :hook
  ((prog-mode text-mode) . display-line-numbers-mode)
  :custom
  (display-line-numbers-width-start t)
  (display-line-numbers-type 'visual))
#+end_src

I like seeing TODO and related keywords highlighted in all buffers. [[https://github.com/tarsius/hl-todo][hl-todo]] to the rescue!

#+begin_src emacs-lisp
(igloo-use-package hl-todo
  :straight t
  :hook
  ((prog-mode text-mode) . hl-todo-mode))
#+end_src

When writing a lot of lisp, it makes sense to highlight parentheses. I spent quite some time figuring out how I want the matching parenthesis to look, and I would have preferred a box around it, but there is no way to do that without having the text jerk around the screen (see [[http://emacs.1067599.n8.nabble.com/bug-13011-24-2-Text-flickering-moving-cursor-with-box-around-text-enabled-td270885.html][this bug report]]).

#+begin_src emacs-lisp
(igloo-use-package paren
  :demand
  :custom
  (show-paren-delay 0)
  :preface
  (declare-function color-lighten-name "color")
  (declare-function color-darken-name "color")
  :config
  (igloo-run-with-frontend
   (require 'color)
   (dolist (desc `((show-paren-match
                    :foreground
                    ,(face-attribute 'cursor :background)
                    :background
                    ,
                    <<paren-match-bg>>
                    :slant italic)
                   (show-paren-mismatch
                    :foreground nil)))
     (let ((face (car desc))
           (spec `((t . ,(cdr desc)))))
       (face-spec-set face spec))))
  (show-paren-mode))
#+end_src

This snippet generates the background color for the matching parenthesis background. The color is lightened slightly, because if the dark version was totally black, then the terminal UI would render it using the palette, e.g. the terminal background, instead of the RGB components.

#+name: paren-match-bg
#+begin_src emacs-lisp :tangle no
(let ((bg (face-attribute 'default :background)))
  (if (color-defined-p bg)
      (color-lighten-name
       (color-darken-name
        (face-attribute 'default :background)
        20)
       1)
    bg))
#+end_src

** Startup

I like seeing a startup message that tells me how long Emacs took to start up (lo and behold the sub-0.5s startup times).

#+begin_src emacs-lisp
(add-hook 'emacs-startup-hook
  (lambda ()
    (message "Emacs ready in %ss with %d garbage collections taking up %ss"
              (float-time (time-subtract after-init-time before-init-time))
              gcs-done gc-elapsed)))
#+end_src

** Font

/Note that not all the source blocks are exported as-is. The first three blocks are tangled into the =igloo-run-with-frontend= invocation at the bottom, using noweb syntax./

The font itself is already set during [[(default-font)][early init]].

To tell Emacs to use different fonts, one needs to specify on which characters those fonts should be used.

For emojis, I use the [[https://github.com/googlefonts/noto-emoji][Noto Color Emoji]] font. I'm not very sure about the codepoint ranges, if anyone wants to check then feel free, but this seems to work.

#+name: setup-emoji
#+begin_src emacs-lisp :tangle no
  (let ((ranges '((#x1f000 . #x1f64f)
                  (#x1f900 . #x1f9ff))))
    (dolist (emojis ranges)
      (set-fontset-font t emojis (font-spec :family "Noto Color Emoji"))))
#+end_src

For symbols, I use Symbols Nerd Font. The codepoint ranges are taken from [[https://github.com/ryanoasis/nerd-fonts/wiki/Glyph-Sets-and-Code-Points][the project wiki]].

#+name: setup-symbols
#+begin_src emacs-lisp :tangle no
(let ((ranges '(;; Seti-UI + Custom
                (#xe5fa . #xe62b)
                ;; Devicons
                (#xe700 . #xe7c5)
                ;; Font Awesome
                (#xf000 . #xf2e0)
                ;; Font Awesome Extension
                (#xe200 . #xe2a9)
                ;; Material Design Icons
                (#xf500 . #xfd46)
                ;; Weather
                (#xe300 . #xe3eb)
                ;; Octicons
                (#xf400 . #xf4a8)
                #x2665 #x26a1 #xf27c
                ;; Powerline Extra Symbols
                (#xe0b4 . #xe0c8)
                (#xe0cc . #xe0d2)
                #xe0a3 #xe0ca #xe0d4
                ;; IEC Power Sybols
                (#x23fb . #x23fe) #x2b58
                ;; Font Logos
                (#xf300 . #xf313)
                ;; Pomicons
                (#xe000 . #xe00d))))
  (dolist (syms ranges)
    (set-fontset-font t syms "Symbols Nerd Font")))
#+end_src

*** Ligatures

Emacs does not do ligatures automatically, so one has to define a bunch of regexes (regexi?) to specify which character sequences have a change of being merged together. The list of ligatures was initially taken from the JetBrains website, then a [[https://github.com/JetBrains/JetBrainsMono/wiki/List-of-supported-symbols][wiki page]] was created, but it doesn't seem up to date, so this is updated on a best-effort basis.

#+name: setup-ligatures
#+begin_src emacs-lisp :tangle no
(let ((alist '(;;  -> -- --> ->> -< -<< --- -~ -|
               (?- . ".\\(?:--\\|[->]>?\\|<<?\\|[~|]\\)")

               ;; // /* /** /// //= /= /== />
               ;; /* cannot be conditioned on patterns followed by a whitespace,
               ;; because that would require support for lookaheads in regex.
               ;; We cannot just match on /*\s, because the whitespace would be considered
               ;; as part of the match, but the font only specifies the ligature for /* with
               ;; no trailing characters
               ;;
               (?/ . ".\\(?:/[=/]?\\|==?\\|\\*\\*?\\|[>]\\)")

               ;; */ *>
               ;; Prevent grouping of **/ as *(*/) by actively looking for **/
               ;; which consumes the triple but the font does not define a substitution,
               ;; so it's rendered normally
               (?* . ".\\(?:\\*/\\|[>/]\\)")

               ;; <!-- <<- <- <-- <=> <= <| <|| <||| <|> <: <:< <> <-< <<< <=< <<= <== <==>
               ;; <~> << <-| <=| <~~ <~ <$> <$ <+> <+ <*> <* </ </> <->
               (?< . ".\\(?:==>\\|!--\\|~~\\|-[|<-]\\||>\\||\\{1,3\\}\\|<[=<-]?\\|=[><|=]?\\|[*+$~/-]>?\\|:<?\\|>\\)")

               ;; := ::= :?> :? :: ::: :< :>
               (?: . ".\\(?:\\?>\\|:?=\\|::?\\|[>?<]\\)")

               ;; == =:= === => =!= =/= ==> =>> =:
               (?= . ".\\(?:[=>]?>\\|[:=!/]?=\\|:\\)")

               ;; != !== !!
               (?! . ".\\(?:==?\\|!\\)")

               ;; >= >> >] >: >- >-> >>> >>= >>- >=>
               ;; >=< should not have a ligature
               (?> . ".\\(?:[=-]>\\|>[=>-]\\|[]:>-]\\|=<?\\)")

               ;; && &&&
               (?& . ".&&?")

               ;; || ||| |> ||> |||> |] |} |-> |=> |- ||- |= ||=
               (?| . ".\\(?:||>\\|[-=|]>\\||[-=|]?\\|[]>}=-]\\)")

               ;; ... .. .? .= ..<
               (?. . ".\\(?:\\.[.<]?\\|[.?=]\\)")

               ;; ++ +++ +>
               (?+ . ".\\(?:\\+\\+?\\|>\\)")

               ;; [| [<
               (?\[ . ".[|<]")

               ;; {|
               (?{ . ".|")

               ;; ?: ?. ?? ??? ?=
               (?? . ".\\(?:[:.=]\\|\\?\\??\\)")

               ;; ## ### #### #{ #[ #( #? #_ #_( #: #! #=
               (?# . ".\\(?:#\\{1,3\\}\\|_(?\\|[{[(?:=!]\\)")

               ;; ;; ;;;
               (?\; . ".;;?")

               ;; __ _|_
               (?_ . ".|?_")

               ;; ~~ ~~> ~> ~- ~@
               (?~ . ".\\(?:~>\\|[>@~-]\\)")

               ;; $>
               (?$ . ".>")

               ;; ^=
               (?^ . ".=")

               ;; ]#
               (?\] . ".#")

               ;; @_
               (?@ . "._")
               )))
  (dolist (char-regexp alist)
    (set-char-table-range composition-function-table (car char-regexp)
                          `([,(cdr char-regexp) 0 font-shape-gstring]))))
#+end_src

*** CJK characters

CJK characters are supposed to be visually 2 characters wide in a monospace font. I mix and match different fonts for CJK and non-CJK characters, so a bit of fiddling with the font size is necessary to keep this assumption. Otherwise, be prepared to look at unaligned tags in org-mode ðŸ˜±.

The reason why it's a minor mode and not a constant is that when changing font size in a buffer with =text-scale-adjust=, only the characters with the default fontspec get resized.

#+name: setup-cjk
#+begin_src emacs-lisp
(defcustom igloo-cjk-chars-size 20
  "Font size for rendering CJK characters in `igloo-scaled-cjk-chars'."
  :group 'igloo-cjk
  :type 'number)
(define-minor-mode igloo-scaled-cjk-chars
  "Minor mode for displaying CJK characters in a bigger size
than the surrounding text."
  :global t
  :lighter nil
  :group 'igloo-cjk
  (let ((fontspec (when igloo-scaled-cjk-chars
                    (font-spec :family "Noto Sans JP" :size igloo-cjk-chars-size))))
    (dolist (charset '(cjk-misc kana bopomofo han kanbun))
      (set-fontset-font t charset fontspec))))
#+end_src

*** Loading with frontend

#+begin_src emacs-lisp
(igloo-run-with-frontend
  <<setup-emoji>>
  <<setup-symbols>>
  <<setup-ligatures>>
  (igloo-scaled-cjk-chars))
#+end_src

** Modeline

This one is pretty easy - I extracted my modeline configuration to [[https://github.com/VojtechStep/vs-modeline.el][a separate repo]], so it's enough to just download it.

#+begin_src emacs-lisp
(igloo-use-package vs-modeline
  :straight (:type git
             :host github
             :repo "VojtechStep/vs-modeline.el")
  :demand
  :config
  (vs-modeline-mode))
#+end_src

** Selection framework

Selection framework is a framework for selecting /things/ - the things can be commands when running =execute-command= (~M-x~), files when running =find-file= (~C-x C-f~), symbols when running =describe-symbol= (~C-h o~), or anything, really.

From the many options available, [[https://emacs-helm.github.io/helm/][helm]], [[https://github.com/abo-abo/swiper][Ivy]], [[https://www.gnu.org/software/emacs/manual/html_mono/ido.html][ido]] and [[https://github.com/raxod502/selectrum][selectrum]] seem to be the most popular. I've only tried helm and selectrum, and while both seem capable of being one's daily driver, I prefer selectrum. It works as a drop-in replacement for the built-in completion framework, so many commands use it automatically. Helm, on the other hand, requires reimplementations of these features, which makes it (in my opinion) a little bloated.

I set =minibuffer-follows-selected-frame= to nil, which makes minibuffers bound to the frame they were invoked in. Also sprinkle a little evil bindings on the minibuffer operations.

#+begin_src emacs-lisp
(igloo-use-package selectrum
  :straight t
  :custom
  (minibuffer-follows-selected-frame nil)
  :custom-face
  (selectrum-current-candidate ((t (:weight bold
                                    :background "#373b47"
                                    :foreground "#ffffff"))))
  :general
  (selectrum-minibuffer-map
   "C-v" #'yank
   "C-w" #'evil-delete-backward-word
   "C-j" #'selectrum-next-candidate
   "C-k" #'selectrum-previous-candidate)
  :preface
  (declare-function selectrum-mode "selectrum")
  :init
  (selectrum-mode))
#+end_src

[[https://github.com/raxod502/prescient.el][prescient]] is a package for sorting and filtering the list of candidates shown by the selection framework. Another popular alternative is [[https://github.com/oantolin/orderless][orderless]], which seems to have better performance at the expense of having less powerful queries (last time I checked).

Prescient also sorts the candidates according to the frequency of their use, so make this information persistent in a file somewhere.

#+begin_src emacs-lisp
(igloo-use-package prescient
  :straight t
  :custom
  (prescient-filter-method '(literal initialism fuzzy))
  :preface
  (declare-function prescient-persist-mode "prescient")
  :config
  (prescient-persist-mode))
#+end_src

This package is a glue for integrating prescient into selectrum. The =:after= clause only specifies =selectrum=, because this package will load prescient by itself.

#+begin_src emacs-lisp
(igloo-use-package selectrum-prescient
  :after selectrum
  :straight t
  :demand
  :custom-face
  (selectrum-prescient-primary-highlight ((t (:foreground "#b5bd68"))))
  (selectrum-prescient-secondary-highlight ((t (:foreground "#8abeb7"))))
  :config
  (selectrum-prescient-mode))
#+end_src

** Consult

Remember a few paragraphs above, when I complained about Helm being bloated with its own reimplementations of some built-in pickers? Well, turns out it also contains some functionality which is /not/ built-in, and that's where [[https://github.com/minad/consult][consult]] comes in. It implements many of these commands, but on top of the Emacs API, instead of a specific selection system, so it can be used with any system that plugs into the default =completing-read=. That's *E X T E N S I B I L I T Y* baby!

As for the configuration, I bind some keys to actions I use often, and I make consult use projectile's project discovery to correctly search the entire project, not only the =default-directory= of the open buffer.

#+begin_src emacs-lisp
(igloo-use-package consult
  :straight t
  :custom
  (xref-show-xref-function #'consult-xref)
  (xref-show-definitions-function #'consult-xref)
  (consult-project-root-function #'projectile-project-root)
  :general
  ('(motion insert) override
   "C-f" #'consult-ripgrep)
  (igloo-leader
   "gg" '(consult-buffer :wk "switch to buffer"))
  (igloo-local-leader
   :keymaps '(outline-mode-map outline-minor-mode-map)
   "g" '(consult-outline :wk "go to heading"))
  :preface
  (declare-function projectile-project-root "projectile"))
#+end_src

* Project management

[[https://github.com/bbatsov/projectile][projectile]] is a one-stop solution for project management. It keeps track of project directories in your system, distinguishes project types, allows one to specify build, test and run commands, you name it.

** Configuration
:PROPERTIES:
:header-args: :tangle no :noweb-ref projectile-config
:END:

Let's start off with the risky one - when =compilation-read-command= is nil, then Emacs does not ask for user confirmation for running the compilation command for the buffer. I turn it off, because it slows me down when I'm working on my projects, and if some code I downloaded off the internetz wants to change the compilation command with something like a dirlocal variable, then Emacs asks whether it's ok to change it.

#+begin_src emacs-lisp
(compilation-read-command nil)
#+end_src

By default, projectile tries to auto-detect the "best" selection framework available for its user-querying operations. Since selectrum hooks itself into the most basic level, projectile skips it and tries to use a more "advanced" completion systems that are built-in, so force it to use the default.

#+begin_src emacs-lisp
(projectile-completion-system 'default)
#+end_src

This is just for convenience - sometimes I might have a buffer open, but not realize to which project it's bound, and when I want to open a file from the same project, I find it confusing when it doesn't show up when calling =projectile-switch-project=. So keep the current project in the candidates.

#+begin_src emacs-lisp
(projectile-current-project-on-switch 'keep)
#+end_src

When projectile looks for files, it correctly detects that I have [[https://github.com/sharkdp/fd][fd]] installed on my system (and you should too), but by default it hides dotfiles, and uses the slower =git ls-files= in git repositories, so change that.

#+begin_src emacs-lisp
(projectile-generic-command "fd . -H0 --type f --color=never")
(projectile-git-command "fd . -H0E .git --type f --color=never")
#+end_src

** Installation

There is one deviation from the conventions, and that is putting the keybinding for =igloo-ff-dwim= in the =:config= rather then the =:general= clause. This seems to be required to silence the byte compiler because of some interaction between use-package, general, and the byte compiler itself, because a hypothetically unreachable form =(autoload igloo-ff-dwim "projectile")= is generated, and then the compilation complains that it is declared twice.

#+begin_src emacs-lisp
(igloo-use-package projectile
  :straight t
  :commands projectile-project-root
  :custom
  <<projectile-config>>
  :general
  (igloo-leader
   "p" '(nil :wk "projectile")
   "po" '(projectile-switch-project :wk "open project")
   "pka" '(projectile-kill-buffers :wk "close all buffers")
   "pd" '(projectile-dired :wk "project dired"))
  :preface
  (declare-function projectile-project-p "projectile")
  (declare-function projectile-register-project-type "projectile")
  (defun igloo-ff-dwim ()
    (interactive)
    (require 'projectile)
    (call-interactively
     (if (projectile-project-p)
         #'projectile-find-file
       #'find-file)))
  :init
  (igloo-leader
   "f" #'(igloo-ff-dwim :wk "files in project"))
  :config
  <<projectile-project-types>>
  (projectile-mode))
#+end_src

** Project types
:PROPERTIES:
:header-args: :tangle no :noweb-ref projectile-project-types
:END:

projectile already comes with many project types predefined, but Zig projects are not one of them, so add it manually.

*** Zig

#+begin_src emacs-lisp
(projectile-register-project-type
 'zig '("build.zig")
 :project-file "build.zig"
 :compile "zig build"
 :run "zig build run")
#+end_src

** Direnv

[[https://direnv.net/][direnv]] is in concept similar to Emacs' dirlocals, except instead of setting buffer-local variables, it sets environment variables, and it's not Emacs specific - you can hook it up to your terminal, so that the environment changes when you =cd= into it. It's super handy when working with things like nix-shell or (heaven forbid) Python virtualenvs.

#+begin_src emacs-lisp
(igloo-use-package direnv
  :straight t
  :demand
  :custom
  (direnv-always-show-summary nil)
  :config
  (direnv-mode))
#+end_src

* Input

** Unicode

This snippet allows me to type ~C-S-u~ and a hexadecimal value of a Unicode codepoint to insert it. This is apparently the norm for GTK, as it works in Chromium-derived browsers (I use Brave), and these are the only two applications where I find it relevant to input Unicode.

I would also prefer it to be inside a use-package form, but I'm pretty sure that would require the funcionality to be extracted into a =require='able form (which is understandable).

#+begin_src emacs-lisp
(defconst igloo--input-unicode-map
  (let ((keymap (make-sparse-keymap)))
    (define-key keymap (kbd "SPC") #'exit-minibuffer)
    (define-key keymap (kbd "C-g") #'abort-recursive-edit)
    keymap))
(defun igloo-input-unicode ()
  (interactive)
  (let ((hex (read-from-minibuffer "" nil igloo--input-unicode-map)))
    (self-insert-command 1 (string-to-number hex 16))))
(general-def 'insert 'override
  "C-S-u" #'igloo-input-unicode)
#+end_src

** Japanese

I'm learning Japanese, so sometimes I need to input Japanese text. I tried [[https://github.com/skk-dev/ddskk][ddskk]] for a while, but I found the input method unintuitive, and the documentation is in Japanese, so I switched to [[https://github.com/google/mozc][mozc]], which is the open source version of Google Japanese Input.

One needs to install the mozc server and Emacs helper independently of the extension. I use a customized version of [[https://aur.archlinux.org/packages/mozc-ut-common/][mozc-ut-common]], which builds the server, the Emacs helper, copies the mozc.el package to =/usr/share/emacs/site-lisp/mozc/mozc.el=, byte compiles it, generates autoloads, and doesn't depend on Qt. The PKGBUILD can be found [[file:extras/mozc-ut-common/PKGBUILD][here]].

#+begin_src emacs-lisp
(igloo-use-package mozc
  :load-path "/usr/share/emacs/site-lisp/mozc"
  :custom
  (mozc-candidate-style 'echo-area)
  (default-input-method "japanese-mozc")
  (mozc-leim-title "Aã‚")
  :preface
  (declare-function mozc-mode "mozc")
  (declare-function find-library-name "find-func")
  :init
  (require 'mozc-autoloads)
  :config
  (when (and (fboundp 'native-compile-async)
             (not (subr-native-elisp-p (symbol-function #'mozc-mode))))
    (require 'find-func)
    (native-compile-async (find-library-name "mozc") nil t)))
#+end_src

** Spell checking

I use the builtin flyspell package with [[http://aspell.net/][aspell]]. There is not a lot of customization going on, since the out-of-the-box config works well enough, but I still haven't figured out how to prevent it from putting its files into =~=, even with =ispell-cmd-args=.

#+begin_src emacs-lisp
(igloo-use-package ispell)
#+end_src

* Evil

Emacs is a great operating system without a decent text editor, amirite fellas ðŸ˜‚. Up top ðŸ¤£!

** Configuration
:PROPERTIES:
:header-args: :tangle no :noweb-ref evil-config
:END:

Reminder: the following forms are not exported as-is, but are included in the =:custom= section of the =igloo-use-package= form under the next heading.

I can't think of a time when I wanted to replace only the first occurrence on a line, therefore make the global substitution the default.

#+begin_src emacs-lisp
(evil-ex-substitute-global t)
#+end_src

I occasionally use =visual-mode=, mostly for org files, so it's convenient to have Evil treat visual lines like it would normal lines - so for example ~D~, ~C~ and vertical movement work as one might expect.

#+begin_src emacs-lisp
(evil-respect-visual-line-mode t)
#+end_src

Make ~<~ and ~>~ respect the configured tab width.

#+begin_src emacs-lisp
(evil-shift-width tab-width)
#+end_src

I often use ~*~ to search for symbols in elisp, so make it look up symbols instead of words.

#+begin_src emacs-lisp
(evil-symbol-word-search t)
#+end_src

Make ~Y~ consistent with other capital-single-letter commands: yank to the end of line. There is always ~yy~ for yanking the whole line.

#+begin_src emacs-lisp
(evil-want-Y-yank-to-eol t)
#+end_src

Evil can setup some keybindings for other modes as well - turn it off here, because we will be using =evil-collection= instead a little later.

#+begin_src emacs-lisp
(evil-want-keybinding nil)
#+end_src

I prefer the Emacs undo heuristics to Evil's "what happens in insert mode, stays in insert mode". Also, since this is running on Emacs 28, use its new undo/redo primitives, because as beautiful as undo-tree is, it is still buggy and I don't find myself using the history explorer as often as I thought I would.

#+begin_src emacs-lisp
(evil-want-fine-undo t)
(evil-undo-system 'undo-redo)
#+end_src

This setting somehow makes more sense to my brain when operating with visual mode around bol/eol.

#+begin_src emacs-lisp
(evil-want-visual-char-semi-exclusive t)
#+end_src

The state is shown in the modeline, no need to echo it.

#+begin_src emacs-lisp
(evil-echo-state nil)
#+end_src

** Installation

Evil is one of the few packages that loads eagerly. It also contributes ~30% to the startup time, so it might be worth investigating how to make it load faster.

#+begin_src emacs-lisp
(eval-when-compile (defvar evil-want-keybinding nil))
(igloo-use-package evil
  :straight t
  :demand
  :custom
  <<evil-config>>
  (evil-lookup-func #'helpful-at-point)
  :general
  (override
   (igloo-lcag "h") #'evil-window-left
   (igloo-lcag "j") #'evil-window-down
   (igloo-lcag "k") #'evil-window-up
   (igloo-lcag "l") #'evil-window-right
   (igloo-lcag "S-h") #'evil-window-move-far-left
   (igloo-lcag "S-j") #'evil-window-move-very-bottom
   (igloo-lcag "S-k") #'evil-window-move-very-top
   (igloo-lcag "S-l") #'evil-window-move-far-right)
  (:states 'motion
   "j" #'evil-next-visual-line
   "k" #'evil-previous-visual-line
   "L" #'evil-end-of-line-or-visual-line
   "H" #'evil-first-non-blank-of-visual-line
   ";" #'evil-ex)
  (minibuffer-local-map
   "C-v" #'yank
   "C-w" #'evil-delete-backward-word)
  (igloo-leader
    "SPC" #'evil-switch-to-windows-last-buffer)
  :preface
  (declare-function evil-repeat-type "evil-repeat")
  (declare-function evil-normalize-keymaps "evil-core")
  (declare-function evil-state-property "evil-common")
  (declare-function evil-refresh-cursor "evil-common")
  (declare-function evil-local-mode "evil-core")
  (declare-function helpful-at-point "helpful")
  :config
  (evil-mode))
#+end_src

*** Sidenote

Why is it necessary to =defvar= =evil-want-keybinding= outside the =use-package= form? If we take a look at the package configuration after macro expansion, we get something like this:

#+begin_example emacs-lisp
(progn
  (use-package-statistics-gather :use-package 'evil nil)
  (straight-use-package 'evil)
  (use-package-statistics-gather :preface 'evil nil)
  (eval-and-compile
    (eval-when-compile
      (with-demoted-errors "Cannot load evil: %S" nil
                           (unless
                               (featurep 'evil)
                             (load "evil" nil t))))
    (declare-function evil-repeat-type "evil-repeat")
    ...)
  (customize-set-variable 'evil-ex-substitute-global t "Customized with use-package evil")
  ...
#+end_example

Now, evil can set up some keybindings for other modes, by =require='ing =evil-keybindings=, which is done automatically if =evil-want-keybinding= is non-nil.

Since we don't want the default keybindings (because we are using evil-collection), we set =evil-want-keybindings= to nil in the =:custom= section. BUT!!! this only happens /after/ evil is loaded, which is too late. use-package loads evil during byte compilation too, so we get a warning from evil-collection that we are not properly disabling evil's keybindings.

There is currently [[https://github.com/jwiegley/use-package/issues/836][no way]] to add custom code to the beginning of the preface while byte-compiling (only =:defines= and =:functions= can go there, but use-package doesn't allow for setting a default value for =defvar=), so the variable needs to be set outside. Big sad ðŸ˜¢.

** Extensions

[[https://github.com/emacs-evil/evil-collection][evil-collection]] is a collection of Evil-themed keybindings for various special modes.

#+begin_src emacs-lisp
(igloo-use-package evil-collection
  :after evil
  :straight t
  :demand)
#+end_src

[[https://github.com/emacs-evil/evil-surround][evil-surround]] allows one to create, change and delete paired delimiters.

#+begin_src emacs-lisp
(igloo-use-package evil-surround
  :after evil
  :straight t
  :demand
  :config
  (global-evil-surround-mode))
#+end_src

[[https://github.com/edkolev/evil-goggles][evil-goggles]] shows a little animation when editing text with operators, to make the user aware of the region the action is applied to. Shorten the duration tho, because I still want it to be snappy.

#+begin_src emacs-lisp
(igloo-use-package evil-goggles
  :after evil
  :straight t
  :demand
  :custom
  (evil-goggles-duration 0.1)
  :config
  (evil-goggles-mode))
#+end_src

[[https://github.com/linktohack/evil-commentary][evil-commentary]] implements operators for (un)commenting a region, which I find familiar from my vim setup.

#+begin_src emacs-lisp
(igloo-use-package evil-commentary
  :after evil
  :straight t
  :demand
  :config
  (evil-commentary-mode))
#+end_src

[[https://github.com/amosbird/evil-numbers][evil-numbers]] adds functions for vim's incrementing and decrementing of numbers under cursor. However, since ~C-x~ is heavily used in Emacs for other purposes, the functions are not bound to any keys by default.

#+begin_src emacs-lisp
(igloo-use-package evil-numbers
  :after evil
  :straight t
  :general
  (:states 'motion
   "C-a" #'evil-numbers/inc-at-pt
   "C-S-a" #'evil-numbers/dec-at-pt))
#+end_src

** Misc modes

These packages have no meaningful configuration on its own (yet), except for evil-collection keybindings. A configuration should be moved out of this section if it were to get more extensive.

#+begin_src emacs-lisp
(igloo-use-package debug
  :preface
  (declare-function evil-collection-debug-setup "modes/debug/evil-collection-debug")
  :config
  (evil-collection-debug-setup))
#+end_src

#+begin_src emacs-lisp
(igloo-use-package calc
  :preface
  (declare-function evil-collection-calc-setup "modes/calc/evil-collection-calc")
  :config
  (evil-collection-calc-setup))
#+end_src

#+begin_src emacs-lisp
(igloo-use-package tabulated-list
  :preface
  (declare-function evil-collection-tabulated-list-setup "modes/tabulated-list/evil-collection-tabulated-list")
  :config
  (evil-collection-tabulated-list-setup))
#+end_src

* Docs

Use an alternative viewer for builtin documentation. There is currently [[https://github.com/Wilfred/helpful/issues/251][an issue]] where the =helpful-= functions might crap out at some positions in the buffer, scrolling a few lines usually fixes it though.

The functions to show and close the help buffer are documented under [[(helpful-show-buffer)][Window layout]]. The ~q~ binding needs to be bound after =evil-collection-helpful-setup=, because evil-collection rebinds it to =quit-window=.

#+begin_src emacs-lisp
(igloo-use-package helpful
  :straight t
  :custom
  (helpful-switch-buffer-function #'igloo--helpful-show-buffer)
  :general
  ([remap describe-function] #'helpful-function
   [remap describe-symbol] #'helpful-symbol
   [remap describe-variable] #'helpful-variable
   [remap describe-key] #'helpful-key)
  :preface
  (declare-function evil-collection-helpful-setup "modes/helpful/evil-collection-helpful")
  :config
  (evil-collection-helpful-setup)
  (general-def 'normal 'helpful-mode-map
    "q" #'igloo--helpful-quit-buffer))
#+end_src

Turn on evil bindings for the default Info viewer.

#+begin_src emacs-lisp
(igloo-use-package info
  :preface
  (declare-function evil-collection-info-setup "modes/info/evil-collection-info")
  :config
  (evil-collection-info-setup))
#+end_src

Show elisp function signatures in the echo area. However, it's supposed to be subtle - one can always pull up the documentation with a single keypress, so don't allow it to expand the echo area to multiple lines.

#+begin_src emacs-lisp
(igloo-use-package eldoc
  :hook
  ((prog-mode) . eldoc-mode)
  :custom
  (eldoc-idle-delay 0.25)
  (eldoc-echo-area-use-multiline-p nil))
#+end_src

* Magit

[[https://github.com/magit/magit][magit]] is arguably the best git experience you can have, integrated into your editing environment.

** Configuration
:PROPERTIES:
:header-args: :tangle no :noweb-ref magit-config
:END:

The configuration provided here is rather bare.

Diffs are not shown when committing, because it pops two frames (one for the commit message, one for the diff), and autofocuses the wrong one, so one has to manually change focus. TODO: this probably can be fixed with =magit-display-buffer-function=.

#+begin_src emacs-lisp
(magit-commit-show-diff nil)
#+end_src

Then, I generally don't want to be asked to save the open files, and I don't want it to be done automatically, so make magit not worry about it.

#+begin_src emacs-lisp
(magit-save-repository-buffers nil)
#+end_src

Finally, magit has keybindings for ~h~ and ~l~, so one can't move around with vim bindings in muscle memory. Evil-collection can fortunately change the bindings so that horizontal movement works as expected.

#+begin_src emacs-lisp
(evil-collection-magit-want-horizontal-movement t)
#+end_src

** Installation

#+begin_src emacs-lisp
(igloo-use-package magit
  :straight t
  :custom
  <<magit-config>>
  :general
  (igloo-leader
    "m" '(nil :wk "magit")
    "mm" '(magit-status :wk "magit status")
    "mf" '(magit-file-dispatch :wk "magit file"))
  :preface
  (declare-function evil-collection-magit-setup "modes/magit/evil-collection-magit")
  :config
  (evil-collection-magit-setup))
#+end_src

** libgit2

There is a work-in-progress, minimal, experimental support for using [[https://github.com/libgit2/libgit2][libgit2]] as the interface for git operations, instead of running the git CLI. It should be faster, but last time I checked only one function was using it, so the difference should be imperceptible, and the only benefit is the cool feeling of running bleeding edge experimental software.

First, install libgit bindings.

#+begin_src emacs-lisp
(igloo-use-package libgit
  :straight t)
#+end_src

And then the magit integration.

#+begin_src emacs-lisp
(igloo-use-package magit-libgit
  :after (magit libgit)
  :straight t)
#+end_src

Note that libgit has to be manually built after installation with =(libgit-load)=. I don't think it can be done automatically when libgit is required, because if magit cannot detect the shared library file, it skips loading libgit.

** Enhancements

With an extension, magit can collect TODO's sprinkled around your git projects, and show them in the status buffer.

#+begin_src emacs-lisp
(igloo-use-package magit-todos
  :after magit
  :straight t
  :hook
  ((magit-mode) . magit-todos-mode)
  :preface
  (declare-function evil-collection-magit-todos-setup "modes/magit-todos/evil-collection-magit-todos")
  :config
  (evil-collection-magit-todos-setup))
#+end_src

* Time tracking

I use [[https://activitywatch.net/][ActivityWatch]] for tracking how I use my time - it's an opensource, local-only alternative to WakaTime and similar solutions.

#+begin_src emacs-lisp
(igloo-use-package activity-watch-mode
  :straight t
  :defer 1
  :config
  (global-activity-watch-mode))
#+end_src

* Email
* Org

** Exports

** Code blocks

#+begin_src emacs-lisp
(add-hook
 'org-src-mode-hook
 (lambda ()
   (when (fboundp 'flycheck-disable-checker)
     (flycheck-disable-checker 'emacs-lisp-checkdoc))))
#+end_src

* RSS Feed

elfeed is a great interface for reading blog posts, especially with a few improvements to reading tech blogs specifically (primarily syntax highlighting). I might even setup elfeed to manage my YouTube feed, if I ever find a way to synchronize subscriptions between elfeed and NewPipe.

All the tweaks are described in the following sections - some are elfeed-specific, others apply to the rendering engine in general.

Also, elfeed puts its database into =~/.elfeed= by default, which is just not right.

#+begin_src emacs-lisp
(igloo-use-package elfeed
  :straight t
  :custom
  (elfeed-feeds
   <<elfeed-feeds>>)
  (igloo-elfeed-ignored
   <<elfeed-ignored>>)
  (elfeed-db-directory (locate-user-emacs-file "elfeed"))
  (elfeed-search-filter "@2-months-ago -junk +unread")
  :general
  (igloo-leader
   "af" #'elfeed)
  :preface
  (declare-function evil-collection-elfeed-setup "modes/elfeed/evil-collection-elfeed")
  (declare-function elfeed-tag-1 "elfeed-db")
  <<elfeed-ignore-funs>>
  :init
  (add-hook 'elfeed-new-entry-hook #'igloo--elfeed-retag-junk)
  :config
  (evil-collection-elfeed-setup))
#+end_src

** Ignoring entries

These are helpers for hiding some entries depending on their tags - instead of trying to write a complex filter, I decided write a configurable predicate that will tag new entries with the =junk= tag. See [[file:room.org.tpl][room.org.tpl]] for details.

#+begin_src emacs-lisp :noweb-ref elfeed-ignore-funs :tangle no
(defcustom igloo-elfeed-ignored nil
  "Alist of mapping from feed id to an ignore pattern."
  :group 'igloo-elfeed
  :type '(choice symbol
                 string
                 (list (choice symbol
                               string))))
(defun igloo--elfeed-matches-ignore (categories pattern)
  (cond
   ((stringp pattern) (member pattern categories))
   ((symbolp pattern) (igloo--elfeed-matches-ignore categories (symbol-name pattern)))
   ((listp pattern) (cl-loop for p in pattern
                             always (igloo--elfeed-matches-ignore categories p)))))
(defun igloo--elfeed-retag-junk (entry)
  (when-let* ((meta (elfeed-entry-meta entry))
              (id (elfeed-entry-feed-id entry))
              (ignored (alist-get id igloo-elfeed-ignored nil nil #'equal))
              (categories (plist-get meta :categories)))
    (when (cl-loop for p in ignored
                   thereis (igloo--elfeed-matches-ignore categories p))
      (elfeed-tag-1 entry 'junk))))
#+end_src

** Simple HTML Renderer

The SHR module is the renderer used by elfeed to show the HTML. It is quite configurable, so here it goes.

*** Slicing images

First of all, showing big images in Emacs buffers can lead to uncomfortable jumping around, specifically when they are scrolled into or out of view. SHR can instead slice up the image, and span it over multiple lines. AFAIK the grid is not configurable (hardcoded to 20 rows and 1 column), so small images suffer somewhat (it takes unexpectedly long to "scroll" through them), but that's fixable. TODO: remove slices for small images.

The only way of forcing this behaviour without replacing the entire =shr-put-image= function is adding the ='(size . original)= flag to the image.

#+begin_src emacs-lisp :noweb-ref shr-preface :tangle no
(defun igloo--shr-put-image-always-slice (original spec alt &optional flags)
  (unless (assq 'size flags)
    (setq flags (cons '(size . original) flags)))
  (funcall original spec alt flags))
#+end_src

*** Local links

Blog posts often have links to other content in the same post - be it footnotes, backreferences, or tables of contents. Unfortunately, shr doesn't seem to natively understand such local links, and instead opens them in the system web browser.

This can be fixed by remapping what =shr-browse-url= does on such a link - instead of opening the link, it searches for a text with the property =shr-target-id= matching the target of the link. This is achieved with a custom keymap that gets applied to all links beginning with =#=, by overriding how links are rendered.

First, the browsing function - its task is to look at the link under point, then look around the buffer for the tag with the correct id, and move point there.

#+begin_src emacs-lisp :noweb-ref shr-preface :tangle no
(defun igloo--shr-local-href-browse ()
  (interactive)
  (let ((url (get-text-property (point) 'shr-url)))
    (unless (and url
                 (eq (string-to-char url) ?#)
                 (goto-char (point-min))
                 (text-property-search-forward
                  'shr-target-id (substring url 1)))
      (message "No local link under point"))))
#+end_src

Second, the keymap - as mentioned above, this just remaps =shr-browse-url=, and inherits the rest from =shr-map=. This needs to be put in =:config= instead of =:preface=, because it uses =shr-map=.

#+begin_src emacs-lisp :noweb-ref shr-local-href-keymap :tangle no
(defvar igloo--shr-local-href-map
  (let ((map (make-sparse-keymap)))
    (set-keymap-parent map shr-map)
    (define-key map [remap shr-browse-url] #'igloo--shr-local-href-browse)
    map))
#+end_src

And finally, the function to add the keymap to the local links. This function is used in the next section.

#+begin_src emacs-lisp :noweb-ref shr-preface :tangle no
(defun igloo--shr-local-href (a)
  (let ((href (dom-attr a 'href))
        (start (point)))
    (if (not (eq (string-to-char href) ?#))
        (shr-tag-a a)
      (shr-generic a)
      (put-text-property
       start (point)
       'keymap igloo--shr-local-href-map)
      (shr-urlify start href (dom-attr a 'title)))))
#+end_src

*** Image links

With the above configuration, images can span multiple lines. On the other hand, if an image functions as a link, then each of these lines is shown in the =shr-link= face, which is underlined - resulting in images with white lines across them. To fix this, tweak how links that are images are rendered - remove the link from the image, and add a text link with the alt text for its contents.

#+begin_src emacs-lisp :noweb-ref shr-preface :tangle no
(defun igloo--shr-render-a (a)
  (when-let* ((inner (dom-children a))
              (img (and (eq 'img
                            (dom-tag inner))
                        (car inner))))
    (shr-tag-img img)
    (setq a (dom-node 'a
                      (dom-attributes a)
                      (concat
                       "("
                       (or (when-let ((alt (dom-attr img 'alt)))
                             (and (stringp alt)
                                  (not (string-empty-p alt))
                                  alt))
                           "Link")
                       ")"))))
  (igloo--shr-local-href a))
#+end_src

*** =pre= syntax highlight

Another enhancement is adding syntax highlighting to code blocks. The package =shr-tag-pre-highlight= provides a function for rendering a =pre= tag with pretty colors, but it uses the same background as the rest of the buffer - hence the wrapper that adds an overlay.

#+begin_src emacs-lisp
(igloo-use-package shr-tag-pre-highlight
  :after shr
  :straight t
  :demand
  :preface
  (declare-function shr-tag-pre-highlight "shr-tag-pre-highlight")
  (defun igloo--shr-render-pre (pre)
    (require 'org-faces)
    (let ((beg (point)))
      (shr-tag-pre-highlight pre)
      (overlay-put (make-overlay beg (point))
                   'face 'org-block)))
  :config
  (cl-pushnew '(pre . igloo--shr-render-pre)
              shr-external-rendering-functions
              :test #'equal)
  (dolist (pair '(("nix" . nix)
                  ("haskell" . haskell)
                  ("hl" . haskell)))
    (cl-pushnew pair shr-tag-pre-highlight-lang-modes
                :test #'equal)))
#+end_src

*** Installation

Putting it all together - helper functions go into =:preface=, special keymap definition and the image advice into =:config=, and the link renderer is registered in a hook.

#+begin_src emacs-lisp
(igloo-use-package shr
  :hook
  ((elfeed-show-mode)
   . (lambda ()
       (setq-local shr-external-rendering-functions
                   (append
                    '((a . igloo--shr-render-a))
                    shr-external-rendering-functions))))
  :preface
  (declare-function shr-tag-a "shr")
  (declare-function shr-tag-img "shr")
  (declare-function shr-put-image "shr")
  (declare-function shr-urlify "shr")
  (declare-function text-property-search-forward "text-property-search")
  (declare-function dom-node "dom")
  <<shr-preface>>
  :config
  <<shr-local-href-keymap>>
  (advice-add #'shr-put-image :around #'igloo--shr-put-image-always-slice))
#+end_src

* Footer

Check the correctness of all declared functions.

#+begin_src emacs-lisp
(eval-when-compile
  (when byte-compile-current-file
    (check-declare-file byte-compile-current-file)))
#+end_src

Footer of a proper elisp file.

#+begin_src emacs-lisp
(provide 'init)
;;; init.el ends here
#+end_src

* Footnotes

[fn:1] TODO: This might be solved by somehow figuring out the color at comptime
